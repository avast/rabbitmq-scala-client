import java.util.function.Function

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.4'
        classpath 'com.avast.gradle:gradle-docker-compose-plugin:0.6.13'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.17.0'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

apply plugin: 'idea'
apply plugin: 'docker-compose'

group = "com.avast.clients.rabbitmq"
version = version == 'unspecified' ? 'DEVELOPER-SNAPSHOT' : version

subprojects {
    apply plugin: 'java'
    apply plugin: 'scala'
    apply plugin: 'com.github.ben-manes.versions'
    apply plugin: 'com.jfrog.bintray'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'

    task sourceJar(type: Jar) {
        from sourceSets.main.allSource
        classifier "sources"
    }

    group = "com.avast.clients.rabbitmq"

    publishing {
        publications {
            MyPublication(MavenPublication) {
                from components.java
                groupId project.group
                version project.version
                artifact sourceJar
            }
        }
    }

    artifacts {
        archives jar
        archives sourceJar
    }

    bintray {
        user = System.getenv('BINTRAY_USER')
        key = System.getenv('BINTRAY_KEY')
        publish = true // auto publish
        configurations = ['archives']

        pkg {
            repo = 'maven'
            name = 'rabbitmq-scala-client'
            userOrg = 'avast'
            desc = 'Scala wrapper over standard RabbitMQ Java client library'
            licenses = ['Apache-2.0']
            vcsUrl = 'https://github.com/avast/rabbitmq-scala-client.git'
            githubRepo = 'avast/rabbitmq-scala-client'
            websiteUrl = 'https://github.com/avast/rabbitmq-scala-client'
            issueTrackerUrl = 'https://github.com/avast/rabbitmq-scala-client/issues'
            labels = ['rabbitmq', 'client', 'amqp', 'scala']
            version {
                name = project.version
                vcsTag = project.version
            }
        }
    }

    configurations {
        scalaCompilerPlugin
    }

    compileScala {
        scalaCompileOptions.additionalParameters = ['-target:jvm-1.8']
    }

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    repositories {
        mavenCentral()
        jcenter()
    }

    ext {
        scalaVersion = "2.12"
        scalaVersionFull = "2.12.7"
        metricsVersion = "2.4.4"
        bytesVersion = "2.0.3"
        circeVersion = "0.10.1"
        cactusVersion = "0.12.5"
        monixVersion = "3.0.0-RC1"
        catsVersion = "1.2.0"
    }

    dependencies {
        scalaCompilerPlugin "org.spire-math:kind-projector_$scalaVersion:0.9.7"

        testCompile "com.avast.metrics:metrics-jmx:${metricsVersion}"

        testCompile "org.scalaj:scalaj-http_$scalaVersion:2.3.0"
        testCompile "io.circe:circe-core_$scalaVersion:$circeVersion"
        testCompile "io.circe:circe-generic_$scalaVersion:$circeVersion"
        testCompile "io.circe:circe-parser_$scalaVersion:$circeVersion"

        testCompile 'junit:junit:4.12'
        testCompile "org.scalatest:scalatest_$scalaVersion:3.0.1"
        testCompile 'org.mockito:mockito-all:1.10.19'
        testCompile 'ch.qos.logback:logback-classic:1.2.3'

        testRuntime 'org.pegdown:pegdown:1.6.0'
    }

    tasks.withType(ScalaCompile) {
        String[] plugins
        plugins = configurations.scalaCompilerPlugin.files.stream().map(new Function() {
            @Override
            Object apply(final Object o) {
                return "-Xplugin:" + ((File) o).getAbsolutePath()
            }
        }).toArray()

        scalaCompileOptions.additionalParameters = plugins
    }

    test {
        testLogging {
            events 'failed'
            exceptionFormat 'full'
        }
    }
}


allprojects {
    //noinspection UnnecessaryQualifiedReference
    tasks.withType(org.gradle.api.tasks.scala.ScalaDoc) {
        enabled = false
    }
}
